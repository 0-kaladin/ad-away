apply plugin: 'android'

dependencies {
    compile fileTree(dir: 'libs', includes: ['*.jar'], excludes: ['android-support-v4.jar'])
    compile 'com.google.android:support-v4:r7'
    compile project(':libraries:Donations')
    compile project(':libraries:actionbarsherlock')
}

android {
    compileSdkVersion 17
    buildToolsVersion "17.0.0"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 17
    }

    /*
     * To sign release build, create file gradle.properties in .gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    signingConfigs {
        myConfig {
            storeFile file(signingStoreLocation)
            storePassword signingStorePassword
            keyAlias signingKeyAlias
            keyPassword signingKeyPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.myConfig
        }
    }

    productFlavors {
        google {
            buildConfig "public final static boolean DONATIONS_GOOGLE = true;"
        }
        fdroid {
            buildConfig "public final static boolean DONATIONS_GOOGLE = false;"
        }
    }
}

/**
 * Android Gradle Plugin 0.4 does not support library packaging.
 * This is a hack from https://groups.google.com/d/msg/adt-dev/Dkf9nsZlq8I/3gtG1Ofu1zAJ
 */
tasks.withType(com.android.build.gradle.tasks.PackageApplication) { pkgTask ->
    pkgTask.jniDir new File(projectDir, 'libs')
}

/**
 * Task to rename executables from hello_world to libhello_world_bin.so
 * If they look like libraries, they are packaged in the apk and deployed on the device in the lib folder!
 *
 * http://www.gradle.org/docs/current/userguide/working_with_files.html
 * Using Sync type instead of Copy to delete original files after renaming.
 */
task renameExecutables(type: Sync) {
    from 'src/main/libs'
    into 'src/main/libs'
    include '**/*'
    exclude '**/*.so'
    exclude '**/*.jar'

    rename(/(.+)/, 'lib$1_bin.so')
}


build.dependsOn renameExecutables